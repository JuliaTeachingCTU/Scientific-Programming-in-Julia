using StatsBase

abstract type Species end

abstract type PlantSpecies <: Species end
abstract type Grass <: PlantSpecies end

abstract type AnimalSpecies <: Species end
abstract type Sheep <: AnimalSpecies end
abstract type Wolf <: AnimalSpecies end

abstract type Agent{S<:Species} end

# instead of Symbols we can use an Enum for the sex field
# using an Enum here makes things easier to extend in case you
# need more than just binary sexes and is also more explicit than
# just a boolean
@enum Sex female male

##########  World  #############################################################

mutable struct World{A<:Agent}
    agents::Dict{Int,A}
    max_id::Int
end

function World(agents::Vector{<:Agent})
    max_id = maximum(a.id for a in agents)
    World(Dict(a.id=>a for a in agents), max_id)
end

# optional: overload Base.show
function Base.show(io::IO, w::World)
    println(io, typeof(w))
    for (_,a) in w.agents
        println(io,"  $a")
    end
end


##########  Animals  ###########################################################

mutable struct Animal{A<:AnimalSpecies} <: Agent{A}
    id::Int
    energy::Float64
    Œîenergy::Float64
    reprprob::Float64
    foodprob::Float64
    sex::Sex
end

function (A::Type{<:AnimalSpecies})(id::Int,E::T,ŒîE::T,pr::T,pf::T,s::Sex) where T
    Animal{A}(id,E,ŒîE,pr,pf,s)
end

# get the per species defaults back
randsex() = rand(instances(Sex))
Sheep(id; E=4.0, ŒîE=0.2, pr=0.8, pf=0.6, s=randsex()) = Sheep(id, E, ŒîE, pr, pf, s)
Wolf(id; E=10.0, ŒîE=8.0, pr=0.1, pf=0.2, s=randsex()) = Wolf(id, E, ŒîE, pr, pf, s)


function Base.show(io::IO, a::Animal{A}) where {A<:AnimalSpecies}
    e = a.energy
    d = a.Œîenergy
    pr = a.reprprob
    pf = a.foodprob
    s = a.sex == female ? "‚ôÄ" : "‚ôÇ"
    print(io, "$A$s #$(a.id) E=$e ŒîE=$d pr=$pr pf=$pf")
end

# note that for new species we will only have to overload `show` on the
# abstract species/sex types like below!
Base.show(io::IO, ::Type{Sheep}) = print(io,"üêë")
Base.show(io::IO, ::Type{Wolf}) = print(io,"üê∫")


##########  Plants  #############################################################

mutable struct Plant{P<:PlantSpecies} <: Agent{P}
    id::Int
    size::Int
    max_size::Int
end

# constructor for all Plant{<:PlantSpecies} callable as PlantSpecies(...)
(A::Type{<:PlantSpecies})(id, s, m) = Plant{A}(id,s,m)
(A::Type{<:PlantSpecies})(id, m) = (A::Type{<:PlantSpecies})(id,rand(1:m),m)

# default specific for Grass
Grass(id; max_size=10) = Grass(id, rand(1:max_size), max_size)

function Base.show(io::IO, p::Plant{P}) where P
    x = p.size/p.max_size * 100
    print(io,"$P  #$(p.id) $(round(Int,x))% grown")
end

Base.show(io::IO, ::Type{Grass}) = print(io,"üåø")

function eat!(sheep::Animal{Sheep}, grass::Plant{Grass}, w::World)
    sheep.energy += grass.size * sheep.Œîenergy
    grass.size = 0
end

########## Eating / Dying / Reproducing  ########################################

function eat!(sheep::Animal{Sheep}, grass::Plant{Grass}, ::World)
    sheep.energy += grass.size * sheep.Œîenergy
    grass.size = 0
end
function eat!(wolf::Animal{Wolf}, sheep::Animal{Sheep}, w::World)
    wolf.energy += sheep.energy * wolf.Œîenergy
    kill_agent!(sheep,w)
end
eat!(::Animal, ::Nothing, ::World) = nothing

kill_agent!(a::Agent, w::World) = delete!(w.agents, a.id)

mates(a::Animal{A}, b::Animal{A}) where A<:AnimalSpecies = a.sex != b.sex
mates(::Agent, ::Agent) = false

function find_mate(a::Animal, w::World)
    ms = filter(x->mates(x,a), w.agents |> values |> collect)
    isempty(ms) ? nothing : sample(ms)
end

function reproduce!(a::Animal{A}, w::World) where {A,S}
    m = find_mate(a,w)
    if !isnothing(m)
        a.energy = a.energy / 2
        vals = [getproperty(a,n) for n in fieldnames(Animal) if n ‚àâ [:id, :sex]]
        new_id = w.max_id + 1
        ≈ù = Animal{A}(new_id, vals..., randsex())
        w.agents[≈ù.id] = ≈ù
        w.max_id = new_id
    end
end



##########  Counting agents  ####################################################

agent_count(p::Plant) = p.size / p.max_size
agent_count(::Animal) = 1
agent_count(as::Vector{<:Agent}) = sum(agent_count,as)

function agent_count(w::World)
    function op(d::Dict,a::A) where A<:Agent
        if A in keys(d)
            d[A] += agent_count(a)
        else
            d[A] = agent_count(a)
        end
        return d
    end
    foldl(op, w.agents |> values |> collect, init=Dict())
end

function find_food(::Animal{Sheep}, w::World)
    as = filter(x -> isa(x,Plant{Grass}), w.agents |> values |> collect)
    isempty(as) ? nothing : sample(as)
end
